## Tag parser -- Given that we have focused on a specific
## Smarty tag (e.g. `{$contact.name|escape}`), we will parse it.

start :=> open tag close .

tag
  :condition    => /if|elseif/ sp expression sp?
  :block_open   => blockname (sp attribute)* sp?
  :block_close  => "/" blockname
  :expression   => expression (sp nofilter)?
  .

expression
   :function       => function "(" sp? function_args? sp? ")"
   :paren          => "(" expression ")"
   :ternary        => expression sp? "?" sp? expression sp? ":" sp? expression
   :math           => expression sp? math sp? expression
   :condition      => condition
   :datum          => datum modifier*
   .

condition
  :not         => "!" expression
  :symbol      => expression sp? compare_symbol sp? expression
  :name        => expression sp compare_name sp expression
  .

modifier  :=> "|" sp? "@"? bareword sp? modifier_attribute* .
modifier_attribute :=> ":" sp? expression sp? .

attribute :=> bareword (sp* "=" sp* expression)? .
datum     :=> (variable | string | number | bareword) .

variable  :=> "$" bareword ("." datum | "->" datum | "[" datum "]")* .
## Unclear -- what are the precedence rules for different kinds of subscripts?

blockname :=> bareword .
nofilter :=> "nofilter" .
function :=> bareword .
function_args :=> expression (sp? "," sp? expression)* .
