start :=> open tag close .

tag
  :condition    => /if|elseif/ sp expression
  :block_open   => blockname (sp attribute)* sp?
  :block_close  => "/" blockname
  :expression   => expression (sp nofilter)?
  .

expression
   :function       => function "(" function_args? ")"
   :paren          => "(" expression ")"
   :ternary        => expression sp? "?" sp? expression sp? ":" sp? expression
   :condition      => condition
   :datum          => datum modifier*
   .

condition
  :not         => "!" expression
  :symbol      => expression sp? operator_2_bool_symbol sp? expression
  :name        => expression sp operator_2_bool_name sp expression
  .

modifier  :=> "|" \s* "@"? bareword \s* modifier_attribute* .
modifier_attribute :=> ":" \s* expression \s* .

attribute :=> bareword (sp* "=" sp* expression)? .
datum     :=> (variable | string | number | bareword) .

variable  :=> "$" bareword ("." datum | "->" datum | "[" datum "]")* .

operator_2_bool_symbol :=>  ("==" | "!=" | ">" | "<" | ">=" | "<=" | "===") .
operator_2_bool_name :=> /and|or|eq|ne/i .
blockname :=> bareword .
nofilter :=> "nofilter" .
function :=> bareword .
function_args :=> expression (sp* "," sp* expression)* .
